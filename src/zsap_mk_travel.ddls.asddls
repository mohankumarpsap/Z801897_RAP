@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Travel Root of RAP BO'
@Metadata.ignorePropagatedAnnotations: true
@Metadata.allowExtensions: true
define root view entity ZSAP_MK_TRavel
  as select from /dmo/travel_m
  composition[0..*] of ZSAP_MK_Booking as _Booking
  association [1]    to /DMO/I_Agency            as _Agency        on $projection.AgencyId = _Agency.AgencyID
  association [1]    to /DMO/I_Customer          as _Customer      on $projection.CustomerId = _Customer.CustomerID
  association [1]    to I_Currency               as _Currency      on $projection.CurrencyCode = _Currency.Currency
  association [1..1] to /DMO/I_Overall_Status_VH as _OverallStatus on $projection.OverallStatus = _OverallStatus.OverallStatus
{
  key travel_id       as TravelId,
      agency_id       as AgencyId,
      customer_id     as CustomerId,
      begin_date      as BeginDate,
      end_date        as EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      booking_fee     as BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      total_price     as TotalPrice,
      currency_code   as CurrencyCode,
      description     as Description,
      overall_status  as OverallStatus,
      @Semantics.user.createdBy: true
      created_by      as CreatedBy,
      @Semantics.systemDateTime.createdAt: true
      created_at      as CreatedAt,
      @Semantics.user.lastChangedBy: true
      last_changed_by as LastChangedBy,
      @Semantics.systemDateTime.lastChangedAt: true
      last_changed_at as LastChangedAt,
      
      
      case when $projection.OverallStatus = 'O' then 'Open'
      when $projection.OverallStatus = 'A' then 'Approved'
      when $projection.OverallStatus = 'R' then 'Rejected'
      when $projection.OverallStatus = 'X' then 'Cancelled'
      else 'No Status'
      end as StatusText,
      
      
      case when $projection.OverallStatus = 'O' then 2
      when $projection.OverallStatus = 'A' then 3
      when $projection.OverallStatus = 'R' then 1
      when $projection.OverallStatus = 'X' then 2
      else 0
      end as criticality,
      
      
      _Agency,
      _Customer,
      _Currency,
      _OverallStatus,
      _Booking
      
}
